# Original model by Michelle Sims:

# converted to run in JAGS - TE
# Random effects for slope and intercept and takes no covariate. 
# Counts are transformed into log(counts) and log(counts) is 
# assumed log-normal distribution.

# 2020-03-10
# 2020-09-10 Modified to make it without covariates

model{
	for(i in 1:N){
		log.count[i] ~ dnorm(log(mu[i]), tau.C[i])
		log(mu[i]) <- ifelse(yearc[i] < year.change,
							a0.1 + a1.1 * yearc[i] + epsilon[i],
							a0.2 + a1.2 * yearc[i] + epsilon[i])

		tau.C[i] <- ifelse(yearc[i] < year.change,
							pow(sigma.C[1], -2),
							pow(sigma.C[2], -2))
		
		# overdispersion part - if sigma.e >> 0, then overdispersed according to the publication
		epsilon[i] ~ dnorm(0, tau.e)    

		loglik[i] <- logdensity.norm(log.count[i], log(mu[i]), tau.C[i])
	}

	a0.1 ~ dnorm(0, 0.01)
	a0.2 ~ dnorm(0, 0.01)
	
	a1.1 ~ dnorm(0, 0.01)  #I(0,) growing part
	a1.2 ~ dnorm(0, 0.01)  #I(,0) shrinking part
	
	#year.change ~ dunif(minT, maxT)   # change point
	year.change ~ dpois(minT + (maxT-minT)/2)


	tau.e <- pow(sigma.e, -2)
	sigma.e ~ dgamma(2, 0.1) #dunif(0, 100)

	#tau.C <- pow(sigma.C, -2)
	for (k in 1:2){
		sigma.C[k] ~ dgamma(2, 0.1)

	}
}
