# Original model by Michelle Sims:
# Model 2: Model with random effects for slope and intercept and latitude quadratic plus 
# overdispersion - this time I did log distance
# adjusted model  - now computes residuals and computes bayesian p-value using deviance

# converted to run in JAGS - TE
# Random effects for slope and intercept and takes 1 covariate. 

# 2020-03-10
# 2020-09-17 Changed to 2 slope model

model{
	for(i in 1:N){
		log.count[i] ~ dnorm(log(mu[i]), tau.C[i])
		log(mu[i]) <- ifelse(yearc[i] < year.change,
							a0.1 + a1.1 * yearc[i] + beta * X[i] + epsilon[i],
							a0.2 + a1.2 * yearc[i] + beta * X[i] + epsilon[i])

		tau.C[i] <- ifelse(yearc[i] < year.change,
							pow(sigma.C[1], -2),
							pow(sigma.C[2], -2))

		# overdispersion part - if sigma.e >> 0, then overdispersed according to the publication
		epsilon[i] ~ dnorm(0, tau.e)    

		loglik[i] <- logdensity.norm(log.count[i], log(mu[i]), tau.C[i])
	}

	a0.1 ~ dnorm(0, 0.01)
	a0.2 ~ dnorm(0, 0.01)
	
	a1.1 ~ dnorm(0, 0.01)  #I(0,)
	a1.2 ~ dnorm(0, 0.01)  #I(,0)
	
	#year.change ~ dunif(minT, maxT)
	year.change ~ dpois(minT + (maxT-minT)/2)

	beta ~ dnorm(0, 0.01)

	tau.e <- pow(sigma.e, -2)
	sigma.e ~ dgamma(2, 0.1) #dunif(0, 100)

	#tau.C <- pow(sigma.C, -2)
	for (k in 1:2){
		sigma.C[k] ~ dgamma(2, 0.1)

	}
}
