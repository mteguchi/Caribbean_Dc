# Original model by Michelle Sims:
# Model 2: Model with random effects for slope and intercept and latitude quadratic plus 
# overdispersion - this time I did log distance
# adjusted model  - now computes residuals and computes bayesian p-value using deviance

# converted to run in JAGS - TE
# Random effects for slope and intercept and takes 2 covariates. 

# 2020-03-10
# 2020-09-17 From Poisson to negative binomial


model{
	for(i in 1:N){
		count[i] ~ dnegbin(p[i], r)

		p[i] <- r/(mu[i] + r)

		log(mu[i]) <- a0 + a1 * yearc[i] + beta[1] * X[i,1] + beta[2] * X[i,2] + epsilon[i]

		# overdispersion part - if sigma.e >> 0, then overdispersed according to the publication
		epsilon[i] ~ dnorm(0, tau.e)    

		loglik[i] <- logdensity.negbin(count[i], p[i], r)

	}

	r ~ dgamma(5, 0.1)

	# a0 and a1 are now "free" parameters because latitude doesn't affect them.
	a0 ~ dnorm(0, 0.01)
	a1 ~ dnorm(0, 0.01)
		
	a0pc <- exp(a0)
	a1pc <- (exp(a1)-1) * 100

	for(k in 1:2){
		beta[k] ~ dnorm(0, 0.01)
		effpc[k] <- (exp(beta[k])-1) * 100
	}
	
	tau.e <- pow(sigma.e, -2)
	sigma.e ~ dgamma(2, 0.5) #dunif(0, 100)
}
