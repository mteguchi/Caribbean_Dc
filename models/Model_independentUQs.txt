
model
{
   for(i in 1:n.states) {
      U[i] ~ dnorm(u_mean,1/(u_sd^2));
      
      tauQ[i] ~ dgamma(q_alpha,q_beta);
      Q[i] <- 1/tauQ[i]; # Note: Q is variance
      
      X0[i] ~ dnorm(x0_mean[i], 1/(x0_sd[i]^2));    # prior on init state
      predX[i,1] <- X0[i] + U[i];  # CB change; EW's code does not include U in X[1]
      X[i,1] <- predX[i,1];}
   
   for(j in 1:n.timeseries) {
      
     tauR[j] ~ dgamma(r_alpha,r_beta);
      R[j] <- 1/tauR[j];  # Note: R is variance
      
      predY[j,1] <- inprod(Z[j,1:n.states], X[,1]);    # modified Z here to pull only cols 1:n.states since added an NA col
      Y[j,1] ~  dnorm(predY[j,1], tauR[j]);}
   
   for(tt in 2:n.yrs) {
      
      for(i in 1:n.states) {
          predX[i,tt] <- X[i,tt-1] + U[i];
          X[i,tt] ~ dnorm(predX[i,tt],tauQ[i]);}
      
      for(j in 1:n.timeseries) {
          predY[j,tt] <- inprod(Z[j,1:n.states], X[,tt]);   # modified Z here to pull only cols 1:n.states since added an NA col
          Y[j,tt] ~  dnorm(predY[j,tt], tauR[j]);}
   }
}
